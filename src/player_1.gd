extends CharacterBody2D
class_name Player

var controller: Node
@export var speed: int = 100
@onready var animatedSprite2D = $AnimatedSprite2D
@onready var direction_pointer_arrow: Node2D = get_node_or_null("Arrow")
@onready var sfx_walk = $sfx_walk
@onready var sfx_crystal = $sfx_crystal
@onready var sfx_dmg = $sfx_dmg




var _health: int = 100


# Get a reference to your main game scene's root node.
# IMPORTANT: Replace "Game" with the actual name of your main game scene's root node
# if it's different (e.g., if your game.tscn's root node is named "MainGameScene").
@onready var game_node_ref = get_tree().get_root().get_node_or_null("Game")

var health: int:
	set(value):
		_health = clamp(value, 0, health_max)
		print("Health set to:", _health)

		if $HealthBar:
			$HealthBar.visible = true  # <â€“â€“ HealthBar jetzt sichtbar
			$HealthBar.update_health(_health, health_max)

		if multiplayer.is_server():
			rpc("update_healthbar_clients", _health, health_max)
			
		await get_tree().create_timer(0.1).timeout # wait shortly so that both players call change the scene
		if _health <= 0 and not dead:
			dead = true
			Globals.playerAlive = false
			if multiplayer.is_server():
				rpc("on_player_dead") # call function for client
				on_player_dead() # call function for host

	get:
		return _health


var health_max: int = 100
var health_min: int = 0
var can_take_damage: bool
var dead: bool

func _enter_tree():
	if Globals.control_mode == Globals.ControlMode.INDIVIDUAL:
		set_multiplayer_authority(name.to_int())

		var updated_spawn_position = Globals.spawn_position
		if not multiplayer.is_server():
			updated_spawn_position.x += 50
		self.position = updated_spawn_position

func _ready():
	# HealthBar zu Beginn ausblenden
	if $HealthBar:
		$HealthBar.visible = false
	
	if is_multiplayer_authority():
		$Camera2D.make_current()

	var mp := get_tree().get_multiplayer()
	if not mp.is_server():
		animatedSprite2D.sprite_frames = load("res://scenes/player/light.tres")

	update_visibility()

	dead = false
	can_take_damage = true
	Globals.playerAlive = true

func update_visibility():
	if Globals.control_mode == Globals.ControlMode.INDIVIDUAL:
		self.visible = is_multiplayer_authority()
	else:
		self.visible = true

func _physics_process(_delta):
	if not dead:
		listen_for_crystal_direction_consume()
		
		var input_vector := Vector2.ZERO

		if Globals.control_mode == Globals.ControlMode.INDIVIDUAL:
			if is_multiplayer_authority():
				input_vector = Input.get_vector("move_left", "move_right", "move_up", "move_down")
		else:
			if not is_multiplayer_authority():
				return
			if controller:
				input_vector = controller.get_combined_input()

		velocity = input_vector * speed

		# ðŸ‘Ÿ Schritt-Sound abspielen
		if input_vector != Vector2.ZERO:
			if not sfx_walk.playing:
				sfx_walk.play()
		else:
			if sfx_walk.playing:
				sfx_walk.stop()

		check_hitbox()

	move_and_slide()
	updateAnimation()


func listen_for_crystal_direction_consume():
	if Input.is_action_just_pressed("consume_crystal_direction_item"):
		if game_node_ref:
			# Call the RPC on the specific Game.gd node instance.
			# The RPC will be sent to the authority of game_node_ref (which is the server).
			game_node_ref.rpc("request_consume_crystal_item", multiplayer.is_server()) # give if host pressed to identify who can see the hint in the end

func is_player():
	return true

func updateAnimation():
	if velocity.length() == 0:
		animatedSprite2D.stop()
	else:
		var direction = "_down"
		if velocity.y > 0 and velocity.x < 0:
			direction = "_hdown"
			animatedSprite2D.flip_h = false
		elif velocity.y > 0 and velocity.x > 0:
			direction = "_hdown"
			animatedSprite2D.flip_h = true
		elif velocity.y < 0 and velocity.x < 0:
			direction = "_hup"
			animatedSprite2D.flip_h = false
		elif velocity.y < 0 and velocity.x > 0:
			direction = "_hup"
			animatedSprite2D.flip_h = true
		elif velocity.x < 0:
			direction = "_horizontal"
			animatedSprite2D.flip_h = false
		elif velocity.x > 0:
			direction = "_horizontal"
			animatedSprite2D.flip_h = true
		elif velocity.y < 0:
			direction = "_up"
		animatedSprite2D.play("move" + direction)

func check_hitbox():
	var hitbox_areas = $PlayerHitbox.get_overlapping_areas()
	var damage: int

	if hitbox_areas:
		var hit_box = hitbox_areas.front()
		var parent = hit_box.get_parent()

		if parent is BatEnemy:
			damage = Globals.batDamageAmount
		if can_take_damage:
			take_damage(damage)

@rpc("authority", "reliable")
func take_damage(damage: int):
	if damage == 0 or dead:
		return

	if sfx_dmg:
		sfx_dmg.play()

	health -= damage
	take_damage_cooldown(1.0)


func take_damage_cooldown(wait_time):
	can_take_damage = false
	await get_tree().create_timer(wait_time).timeout
	can_take_damage = true

@rpc("any_peer", "reliable")
func update_healthbar_clients(_current: int, _max: int):
	if $HealthBar:
		$HealthBar.update_health(_current, _max)

	if _current <= 0:
		on_player_dead()

@rpc("any_peer", "reliable")
func on_player_dead():
	Globals.playerAlive = false
	multiplayer.multiplayer_peer = null # todo check if working
	SceneManager.goto_scene("res://scenes/ui/GameOverMenu.tscn")

@rpc("any_peer", "reliable")
func set_arrow_visibility(isVisible, radius):
	direction_pointer_arrow.rotation = radius
	direction_pointer_arrow.visible = isVisible

@rpc("reliable")
func show_consumption_indicator(host_pressed):

	if not is_instance_valid(direction_pointer_arrow):
		return

	var crystal_nodes = get_tree().get_nodes_in_group("crystals")

	if crystal_nodes.is_empty():
		direction_pointer_arrow.visible = false
		return

	var nearest_crystal_node = null
	var min_distance_squared_to_player = INF

	var player_global_position = self.global_position

	for crystal in crystal_nodes:
		if not is_instance_valid(crystal):
			continue
		var distance_sq = player_global_position.distance_squared_to(crystal.global_position)

		if distance_sq < min_distance_squared_to_player:
			min_distance_squared_to_player = distance_sq
			nearest_crystal_node = crystal

	if is_instance_valid(nearest_crystal_node):
		var arrow_current_global_position = direction_pointer_arrow.global_position
		var crystal_pos = nearest_crystal_node.global_position

		var direction_vector_from_arrow = crystal_pos - arrow_current_global_position

		var target_angle_rad = direction_vector_from_arrow.angle()

		if not host_pressed: # only person who did not press the button sees the arrow
			rpc("set_arrow_visibility", true, target_angle_rad) # for host
		else:
			direction_pointer_arrow.rotation = target_angle_rad  # for client
			direction_pointer_arrow.visible = true

		var timer = get_tree().create_timer(3.0)
		await timer.timeout

		if is_instance_valid(direction_pointer_arrow):
			if not host_pressed: # only person who did not press the button sees the arrow
				rpc("set_arrow_visibility", false, 0) # for host
			else:
				direction_pointer_arrow.visible = false # for client

	else:
		if not host_pressed: # only person who did not press the button sees the arrow
			rpc("set_arrow_visibility", false, 0) # for host
		else:
			direction_pointer_arrow.visible = false # for client
